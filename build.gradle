/*
 * Copyright 2019 NSTDA
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 */

plugins {
    id 'java'
    id 'idea'
    id 'com.palantir.git-version' version '0.11.0'
    id "de.fuerstenau.buildconfig" version "1.1.8"
    id 'edu.sc.seis.launch4j' version '2.4.5'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id "org.jlleitschuh.gradle.ktlint" version "7.2.1"
}

group 'ffc.airsync.launcher'
version gitVersion()

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile 'com.github.lionants02:CheckDuplicate:0.0.4'
    compile 'com.github.lionants02:check46BitSupport:0.0.1'
    compile 'com.github.lionants02:EasyPrintLogGUI:0.0.2'
    compile 'com.github.lionants02:ZIpDownload:0.0.1'
    compile 'com.github.lionants02:GitHubLatestApi:0.0.1'
    compile 'com.github.lionants02:WindowsCreateLink:0.0.2'
    compile 'com.github.lionants02:JvmIsAdmin:0.0.1'
    compile 'com.intellij:forms_rt:7.0.3'

    testImplementation "org.amshove.kluent:kluent:1.34"
    testImplementation "junit:junit:4.12"
}

jar {
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    destinationDirectory = file("$buildDir/bin")

    manifest {
        attributes(
                'Main-Class': 'ffc.airsync.launcher.MainKt',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': "NSTDA"
        )
    }

    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE*'
    exclude 'META-INF/LICENSE*'
    exclude 'about.html'
}

launch4j {
    outfile = "ffc-airsync.exe"
    mainClassName = 'ffc.airsync.launcher.MainKt'
    productName = 'FFC AirSync'
    fileDescription = "Launcher for FFC AirSync application"
    version = project.version
    icon = "$projectDir/logo.ico"
    jreRuntimeBits = '64/32'
    companyName = "NSTDA"
    language = "THAI"
    trademarks = "FFC AirSync"
    copyright = "Apache License 2.0"
}

task createInstaller(type: edu.sc.seis.launch4j.tasks.Launch4jLibraryTask) {
    outfile = 'ffc-airsync-installer.exe'
    mainClassName = 'ffc.airsync.launcher.MainKt'
    icon = "$projectDir/logo.ico"
    fileDescription = "Installer for FFC AirSync"
    manifest = "$projectDir/launch4j.manifest"
    version = project.version
    companyName = "NSTDA"
    language = "THAI"
    trademarks = "FFC AirSync"
    copyright = "Apache License 2.0"
}

build.dependsOn createExe
build.dependsOn createInstaller
